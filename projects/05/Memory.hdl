// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory
 * location specified by address. If load==1, the in value is loaded
 * into the memory location specified by address. This value becomes
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used.
 * Access to address>0x6000 is invalid. Access to any address in
 * the range 0x4000-0x5FFF results in accessing the screen memory
 * map. Access to address 0x6000 results in accessing the keyboard
 * memory map. The behavior in these addresses is described in the
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Select either RAM, Screen, or Keyboard
    DMux4Way(in=load, sel=address[13..14], a=selRamA, b=selRamB, c=selScreen, d=null);

    // Select either of the RAM outputs from above
    Or(a=selRamA, b=selRamB, out=selRam);

    // Select the value stored at the selected memory location
    RAM16K(in=in, load=selRam, address=address[0..13], out=ramOut);

    // The screen chip STARTS addressing at 16384 and stores its data in 8K RAM.
    // Therefore, we start indexing from the 0th address with 13 bits!
    Screen(in=in, load=selScreen, address=address[0..12], out=screenOut);

    // Get the output code of the currently pressed key
    Keyboard(out=kbdOut);

    // Reverse of step 1, i.e., output the stored value and the selected memory location
    Mux4Way16(a=ramOut, b=ramOut, c=screenOut, d=kbdOut, sel=address[13..14], out=out);
}
